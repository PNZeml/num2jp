plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'maven'
    id 'signing'
}

group 'io.github.pnzeml'
archivesBaseName 'num2jp'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:1.3.21"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'num2jp'
                packaging 'jar'
                description 'Converter western numbers to japanese strings'
                url 'https://github.com/PNZeml/num2jp'

                scm {
                    connection 'scm:git:git://github.com/PNZeml/num2jp.git'
                    developerConnection 'scm:git:ssh://github.com/PNZeml/num2jp.git'
                    url 'http://github.com/PNZeml/num2jp/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'PNZeml'
                        name 'Pavel Zemlianikin'
                        email 'pavelz1n@yandex.ru'
                        organization 'jr2'
                        organizationUrl 'https://github.com/lipgen/JR2'
                    }
                }
            }
        }
    }
}